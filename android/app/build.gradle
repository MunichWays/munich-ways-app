def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def mapsProperties = new Properties()
def localMapsPropertiesFile = rootProject.file('local_maps.properties')
if (localMapsPropertiesFile.exists()) {
    project.logger.info('Load maps properties from local file')
    localMapsPropertiesFile.withReader('UTF-8') { reader ->
        mapsProperties.load(reader)
    }
} else {
    project.logger.info('Load maps properties from environment')
    try {
        mapsProperties['MAPS_API_KEY'] = System.getenv('MAPS_API_KEY')
    }catch(NullPointerException e){
        project.logger.warn('Failed to load MAPS_API_KEY from environment.', e)
    }
}
def mapsApiKey = mapsProperties.getProperty('MAPS_API_KEY')
if(mapsApiKey == null){
    mapsApiKey = ""
    project.logger.error('Google Maps Api Key not configured. Set it in `local_maps.properties` or in the environment variable `MAPS_API_KEY`')
}

def keystorePropertiesFile = file('../release_keystore.properties')
def keystoreProperties = new Properties()
if(keystorePropertiesFile.exists()) {
    project.logger.info('Load keystore properties from local file')
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    project.logger.info('Load keystore properties from environment')
    try {
        keystoreProperties['storePassword'] = System.getenv('ANDROID_RELEASE_STORE_PASSWORD')
        keystoreProperties['alias'] = System.getenv('ANDROID_RELEASE_ALIAS')
        keystoreProperties['aliasPassword'] = System.getenv('ANDROID_RELEASE_ALIAS_PASSWORD')
    } catch(NullPointerException e) {
        project.logger.error('Failed to load keystore properties from environment.', e)
    }
}


def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.munichways.app"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders = [MAPS_API_KEY: mapsApiKey]
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['alias']
            keyPassword keystoreProperties['aliasPassword']
            storeFile file("release.keystore")
            storePassword keystoreProperties['storePassword']
        }

    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
